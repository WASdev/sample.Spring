apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'liberty'

group = 'liberty.samples'
version = '1'

description = "Liberty Spring Gradle example"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

buildscript {
    repositories {
        mavenLocal()
        maven {
            name = 'Sonatype Nexus Snapshots'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.0-SNAPSHOT'
    }
}

repositories {
    mavenCentral()   
}
dependencies {
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version:'3.1.1'
    testCompile group: 'org.glassfish', name: 'javax.json', version:'1.0.4'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    providedCompile group: 'org.apache.geronimo.specs', name: 'geronimo-servlet_3.0_spec', version:'1.0'
    providedCompile group: 'javax.jms', name: 'javax.jms-api', version:'2.0'
    libertyRuntime group: 'com.ibm.websphere.appserver.runtime', name: 'wlp-javaee7', version: '17.0.0.2'
    compile group: 'aopalliance', name: 'aopalliance', version:'1.0'
    compile group: 'org.aspectj', name: 'aspectjrt', version:'1.7.1'
    compile group: 'javax.inject', name: 'javax.inject', version:'1'
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version:'1.7.20'
    compile group: 'jstl', name: 'jstl', version:'1.2'
    compile group: 'log4j', name: 'log4j', version:'1.2.17'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.20'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version:'1.7.20'
    compile group: 'org.springframework', name: 'spring-aop', version:'3.2.1.RELEASE'
    compile group: 'org.springframework', name: 'spring-webmvc', version:'3.2.1.RELEASE'
    compile group: 'org.springframework', name: 'spring-expression', version:'3.2.1.RELEASE'
    compile group: 'org.springframework', name: 'spring-web', version:'3.2.1.RELEASE'
    compile group: 'org.springframework', name: 'spring-context', version:'3.2.1.RELEASE'
    compile group: 'org.springframework', name: 'spring-core', version:'4.3.7.RELEASE'
    compile group: 'org.springframework', name: 'spring-beans', version:'3.2.1.RELEASE'
    testCompile group: 'org.glassfish', name: 'javax.json', version:'1.0.4'
}

ext {
    testServerHttpPort = 9811
    testServerHttpsPort = 9812
    warContext = 'sample.spring'
    packagingType = 'usr'
}


war {
    archiveName = baseName + '.' + extension
}
    
liberty {
    server {
        configFile = file('src/main/liberty/config/server.xml')
        bootstrapProperties = ['default.http.port': testServerHttpPort, 
                               'default.https.port': testServerHttpsPort,
                               'appContext': warContext,
                               'appLocation': war.archiveName]
        
        looseApplication = true
    }
    
    packageLiberty {
        include = packagingType
    }
}

test {
    println 'inside the unit test block'
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-results/unit")
    exclude '**/it/**'
}


task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-results/it")
    include '**/it/**'
    exclude '**/unit/**'

    systemProperties = ['liberty.test.port': testServerHttpPort, 'war.context': warContext]
}

task printMessageAboutRunningServer {
    doLast {
        println "The server is now running at http://localhost:${testServerHttpPort}/${warContext}"
        println "To stop the server run 'gradle libertyStop'"
    }
}

libertyStart.dependsOn 'installApps'
libertyStart.finalizedBy 'printMessageAboutRunningServer'
